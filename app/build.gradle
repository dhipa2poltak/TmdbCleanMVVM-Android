plugins {
  id 'com.android.application'
  id 'org.jetbrains.kotlin.android'
  id 'kotlin-kapt'
  id 'dagger.hilt.android.plugin'
}

apply from: "$project.rootDir/jacoco/module.gradle"

android {
  namespace = "com.dpfht.tmdbcleanmvvm"
  compileSdk ConfigData.compileSdkVersion

  defaultConfig {
    applicationId "com.dpfht.tmdbcleanmvvm"

    minSdk ConfigData.minSdkVersion
    targetSdk ConfigData.targetSdkVersion
    versionCode 1
    versionName "1.0"

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  buildTypes {
    release {
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

      manifestPlaceholders.appNameSuffix = ""
    }
    debug {
      applicationIdSuffix ".debug"
      versionNameSuffix "-debug"
      manifestPlaceholders.appNameSuffix = " (debug)"
    }
  }
  buildFeatures {
    buildConfig true
    viewBinding true
    dataBinding true
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
  }
  kotlinOptions {
    jvmTarget = '17'
  }

  flavorDimensions "default"

  productFlavors {
    staging {
      applicationId "com.dpfht.tmdbcleanmvvm.staging"
      manifestPlaceholders.appName = "TMDB Clean MVVM-Staging"

      buildConfigField 'String', 'BASE_URL', STAGING_BASE_URL
    }
    verify {
      applicationId "com.dpfht.tmdbcleanmvvm.verify"
      manifestPlaceholders.appName = "TMDB Clean MVVM-Verify"

      buildConfigField 'String', 'BASE_URL', VERIFY_BASE_URL
    }
    prod {
      applicationId "com.dpfht.tmdbcleanmvvm"
      manifestPlaceholders.appName = "TMDB Clean MVVM"

      buildConfigField 'String', 'BASE_URL', PROD_BASE_URL
    }
  }
}

dependencies {

  implementation fileTree(dir: "libs", include: ["*.jar"])
  implementation project(":framework")
  implementation project(":domain")
  implementation project(":data")
  implementation project(":features:feature_splash")
  implementation project(":features:feature_genre")
  implementation project(":features:feature_movies_by_genre")
  implementation project(":features:feature_movie_details")
  implementation project(":features:feature_movie_reviews")
  implementation project(":features:feature_movie_trailer")
  implementation project(":features:feature_error_message")

  implementation Deps.coreKtx
  implementation Deps.appCompat
  implementation Deps.material
  testImplementation Deps.jUnit
  androidTestImplementation Deps.jUnitExt
  androidTestImplementation Deps.espresso

  implementation Deps.retrofitConverterGson
  implementation Deps.okHttpLoggingInterceptor
  implementation Deps.okHttp

  implementation Deps.navigationFragment
  implementation Deps.navigationUI

  implementation Deps.hilt
  kapt Deps.hiltCompiler

  testImplementation Deps.coroutinesTest
  testImplementation Deps.mockitoKotlin
  testImplementation Deps.coreTesting
  testImplementation Deps.robolectric
}
