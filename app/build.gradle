plugins {
  id 'com.android.application'
  id 'org.jetbrains.kotlin.android'
  id 'kotlin-kapt'
  id 'androidx.navigation.safeargs.kotlin'
  id 'dagger.hilt.android.plugin'
}

android {
  compileSdk 33

  defaultConfig {
    applicationId "com.dpfht.tmdbcleanmvvm"
    minSdk 16
    targetSdk 33
    versionCode 1
    versionName "1.0"

    multiDexEnabled true

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  buildTypes {
    release {
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

      manifestPlaceholders.appNameSuffix = ""
    }
    debug {
      applicationIdSuffix ".debug"
      versionNameSuffix "-debug"
      manifestPlaceholders.appNameSuffix = " (debug)"
    }
  }
  buildFeatures {
    viewBinding true
    dataBinding true
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = '1.8'
  }

  flavorDimensions "default"

  productFlavors {
    staging {
      applicationId "com.dpfht.tmdbcleanmvvm.staging"
      manifestPlaceholders.appName = "TMDB Clean MVVM-Staging"
    }
    verify {
      applicationId "com.dpfht.tmdbcleanmvvm.verify"
      manifestPlaceholders.appName = "TMDB Clean MVVM-Verify"
    }
    prod {
      applicationId "com.dpfht.tmdbcleanmvvm"
      manifestPlaceholders.appName = "TMDB Clean MVVM"
    }
  }
}

dependencies {

  def appDependencies = rootProject.ext.appDependencies

  implementation fileTree(dir: "libs", include: ["*.jar"])
  implementation files("../libjars/YouTubeAndroidPlayerApi.jar")
  implementation project(":framework")
  implementation project(":domain")
  implementation project(":data")
  implementation project(":features:feature_splash")
  implementation project(":features:feature_genre")
  implementation project(":features:feature_movies_by_genre")
  implementation project(":features:feature_movie_details")
  implementation project(":features:feature_movie_reviews")
  implementation project(":features:feature_movie_trailer")
  implementation project(":features:feature_error_message")

  implementation 'androidx.core:core-ktx:1.9.0'
  implementation 'androidx.appcompat:appcompat:1.6.1'
  implementation 'com.google.android.material:material:1.8.0'
  testImplementation 'junit:junit:4.13.2'
  androidTestImplementation 'androidx.test.ext:junit:1.1.5'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

  implementation appDependencies.retrofitConverterGson
  implementation appDependencies.okHttpLoggingInterceptor
  implementation appDependencies.okHttp

  implementation appDependencies.navigationFragment
  implementation appDependencies.navigationUI

  implementation appDependencies.hilt
  kapt appDependencies.hiltCompiler

  implementation appDependencies.multiDex

  testImplementation appDependencies.coroutinesTest
  testImplementation appDependencies.mockitoKotlin
  testImplementation appDependencies.coreTesting
}
